plugins {
    id 'java-library'
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = project.group
description = project.displayname
archivesBaseName = project.name

loom {
    mixin.defaultRefmapName = 'mson.mixin.refmap.json'
    accessWidenerPath = file('src/main/resources/mson.aw')
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

sourceSets {
    test {
       java {
           // gradle and/or loom pls
           exclude '*'
       }
   }
}

repositories {
    maven {
        name = 'modmenu'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modApi "net.fabricmc:fabric-loader:${project.loader_version}"

    modApi fabricApi.module("fabric-api-base", project.fabric_version)
    modApi fabricApi.module("fabric-events-lifecycle-v0", project.fabric_version)
    modApi fabricApi.module("fabric-resource-loader-v0", project.fabric_version)

    modCompileOnly("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
    inputs.property "version", project.version.toString()

    filesMatching("fabric.mod.json") {
        expand "version": project.version.toString()
    }

    from 'LICENSE'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "Mson"
                description = "Jsonify your Minecraft Models"
                url = "https://minelittlepony-mod.com"
                licenses {
                    license {
                        name = "MIT Public License"
                        url = "https://tlo.mit.edu/learn-about-intellectual-property/software-and-open-source-licensing"

                    }
                }
                developers {
                    developer {
                        id = "sollace"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MineLittlePony/Mson.git'
                    developerConnection = 'scm:git:ssh://github.com/MineLittlePony/Mson.git'
                    url = 'https://github.com/MineLittlePony/Mson'
                }
            }
        }
    }

    repositories {
        maven {
            name = "MineLittlePony"
            // https://github.com/ajoberstar/reckon/blob/f4a4608f996b39b3ca6eafe4804c5b38435457c4/reckon-gradle/src/main/java/org/ajoberstar/reckon/gradle/ReckonPlugin.java#L82
            def stage = version.version.stage.value
            url = "s3://repo.minelittlepony-mod.com/maven/${stage != null && stage.name == 'beta' ? 'snapshot' : 'release'}"
            credentials(AwsCredentials) {
                accessKey = System.env.ACCESS_KEY
                secretKey = System.env.SECRET_KEY
            }
        }
    }
}
